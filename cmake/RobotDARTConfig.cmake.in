# - Config file for the robot_dart package
# example:
# find_package(RobotDART REQUIRED)
# add_executable(robot_dart_example example.cpp)
# target_link_libraries(robot_dart_example RobotDART::Simu)

####### Corresponds to @PACKAGE_INIT@  #######

get_filename_component(PACKAGE_PREFIX_DIR "${CMAKE_CURRENT_LIST_DIR}/../../../" ABSOLUTE)

macro(set_and_check _var _file)
  set(${_var} "${_file}")
  if(NOT EXISTS "${_file}")
    message(FATAL_ERROR "File or directory ${_file} referenced by variable ${_var} does not exist !")
  endif()
endmacro()

macro(check_required_components _NAME)
  foreach(comp ${${_NAME}_FIND_COMPONENTS})
    if(NOT ${_NAME}_${comp}_FOUND)
      if(${_NAME}_FIND_REQUIRED_${comp})
        set(${_NAME}_FOUND FALSE)
      endif()
    endif()
  endforeach()
endmacro()

####################################################################################


include(CMakeFindDependencyMacro)
include(FindPackageHandleStandardArgs)

# CMAKE_MODULE_PATH
set(CMAKE_MODULE_PATH "@RobotDART_CMAKE_MODULE_PATH@")

find_package(Eigen3 REQUIRED CONFIG)
if(EIGEN3_VERSION_STRING VERSION_LESS 3.2.92)  # 3.3~beta1
  message(FATAL_ERROR "Eigen3 ${EIGEN3_VERSION_STRING} is found but >= 3.2.92 "
    "(3.3~beta1) is required"
  )
endif()


find_dependency(DART 6.0.0 REQUIRED COMPONENTS utils-urdf @DART_EXTRA_LIBS@ CONFIG)

set(THREADS_PREFER_PTHREAD_FLAG ON)
find_package(Threads REQUIRED)

# Look for components (ie. libraries)
set(RobotDART_USE_MAGNUM FALSE)
set(RobotDART_MAGNUM_REQUIRED QUIET)
if( RobotDART_FIND_COMPONENTS )
    foreach(comp ${RobotDART_FIND_COMPONENTS})
       if ( ${comp} STREQUAL "Magnum")
           if (RobotDART_FIND_REQUIRED_${comp})
               set(RobotDART_MAGNUM_REQUIRED REQUIRED)
           endif()
           message(STATUS "RobotDART: Magnum component requested")
           set(RobotDART_USE_MAGNUM TRUE)
       endif()
    endforeach()
else()
    message(STATUS "No RobotDART optional component specified")
endif()

# add the robot_dart paths (we get them from waf)
set(RobotDART_INCLUDE_DIRS "@RobotDART_INCLUDE_DIRS@")
set(RobotDART_LIBRARY_DIRS "@RobotDART_LIBRARY_DIRS@")

set(RobotDART_LIBRARY ${RobotDART_LIBRARY_DIRS}/libRobotDARTSimu@RobotDART_LIB_TYPE@)
set(RobotDART_LIBRARIES "Threads::Threads;Eigen3::Eigen;${DART_LIBRARIES}")



# Check cmake version
if("${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" LESS 2.6)
   message(FATAL_ERROR "CMake >= 2.6.0 required")
endif()
cmake_policy(PUSH)
cmake_policy(VERSION 2.6...3.20)

# Protect against multiple inclusion, which would fail when already imported targets are added once more.
set(_targetsDefined)
set(_targetsNotDefined)
set(_expectedTargets)
foreach(_expectedTarget RobotDART::Simu RobotDART::Magnum)
  list(APPEND _expectedTargets ${_expectedTarget})
  if(NOT TARGET ${_expectedTarget})
    list(APPEND _targetsNotDefined ${_expectedTarget})
  endif()
  if(TARGET ${_expectedTarget})
    list(APPEND _targetsDefined ${_expectedTarget})
  endif()
endforeach()
if("${_targetsDefined}" STREQUAL "${_expectedTargets}")
  unset(_targetsDefined)
  unset(_targetsNotDefined)
  unset(_expectedTargets)
  set(CMAKE_IMPORT_FILE_VERSION)
  cmake_policy(POP)
  return()
endif()
if(NOT "${_targetsDefined}" STREQUAL "")
  message(FATAL_ERROR "Some (but not all) targets in this export set were already defined.\nTargets Defined: ${_targetsDefined}\nTargets not yet defined: ${_targetsNotDefined}\n")
endif()
unset(_targetsDefined)
unset(_targetsNotDefined)
unset(_expectedTargets)

add_library(RobotDART::Simu INTERFACE IMPORTED)
set_target_properties(RobotDART::Simu PROPERTIES
  INTERFACE_INCLUDE_DIRECTORIES "${RobotDART_INCLUDE_DIRS}"
  INTERFACE_LINK_DIRECTORIES "${RobotDART_LIBRARY_DIRS}"
  INTERFACE_LINK_LIBRARIES "${RobotDART_LIBRARY};${RobotDART_LIBRARIES}"
  INTERFACE_COMPILE_OPTIONS "@RobotDART_CXX_FLAGS@")

if(RobotDART_USE_MAGNUM)
  find_package(Magnum ${RobotDART_MAGNUM_REQUIRED} COMPONENTS @RobotDART_MAGNUM_DEP_LIBS@ CONFIG)
  find_package(MagnumIntegration ${RobotDART_MAGNUM_REQUIRED} COMPONENTS Dart Eigen CONFIG)

  if(Magnum_FOUND)
    set(RobotDARTMagnum_LIBRARY ${RobotDART_LIBRARY_DIRS}/libRobotDARTMagnum@RobotDART_LIB_TYPE@)

    add_library(RobotDART::Magnum INTERFACE IMPORTED)
    set_target_properties(RobotDART::Magnum PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES "${RobotDART_INCLUDE_DIRS}"
        INTERFACE_LINK_DIRECTORIES "${RobotDART_LIBRARY_DIRS}"
        INTERFACE_COMPILE_DEFINITIONS "GRAPHIC;@RobotDART_MAGNUM_DEFINITIONS@"
        INTERFACE_LINK_LIBRARIES "${RobotDARTMagnum_LIBRARY};@RobotDART_MAGNUM_LIBS@")
    set(RobotDART_Magnum_FOUND TRUE)
  else()
    set(RobotDART_Magnum_FOUND FALSE)
  endif()
endif()


# Handle the QUIET and REQUIRED arguments
find_package_handle_standard_args(
    RobotDART #Package name
    DEFAULT_MSG
    # Variables required to evaluate as TRUE
    RobotDART_LIBRARY
    RobotDART_INCLUDE_DIRS
    RobotDART_LIBRARY_DIRS)

mark_as_advanced(RobotDART_INCLUDE_DIRS RobotDART_LIBRARY_DIRS RobotDART_LIBRARY RobotDART_FOUND)

if(CMAKE_VERSION VERSION_LESS 2.8.12)
  message(FATAL_ERROR "This file relies on consumers using CMake 2.8.12 or greater.")
endif()

# Commands beyond this point should not need to know the version.
set(CMAKE_IMPORT_FILE_VERSION)
cmake_policy(POP)
